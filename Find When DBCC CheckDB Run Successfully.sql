/**  FIND WHEN THE LAST TIME DBCC CHECKDB RUN SUCCESSFULLY  **/
SET NOCOUNT ON;
IF OBJECT_ID('tempdb..#temp') IS NOT NULL
	DROP TABLE #temp;

CREATE TABLE #temp (
       Id INT IDENTITY(1,1), 
       ParentObject VARCHAR(255),
       [Object] VARCHAR(255),
       Field VARCHAR(255),
       [Value] VARCHAR(255)
);
 
INSERT INTO #temp
EXECUTE SP_MSFOREACHDB'DBCC DBINFO ( ''?'') WITH TABLERESULTS';
 
;WITH
CHECKDB1 AS
(
    SELECT
		  [Value]
		, ROW_NUMBER() OVER (ORDER BY ID) AS rn1 
	FROM	#temp
	WHERE	Field IN ('dbi_dbname')
),
CHECKDB2 AS
(
	SELECT
		  [Value]
		, ROW_NUMBER() OVER (ORDER BY ID) AS rn2
	FROM	#temp 
	WHERE	Field IN ('dbi_dbccLastKnownGood')
)

SELECT
	  CHECKDB1.Value AS DatabaseName
	, CHECKDB2.Value AS LastRanDBCCCHECKDB
FROM CHECKDB1
	INNER JOIN CHECKDB2 ON rn1 =rn2
WHERE	CHECKDB1.Value NOT IN ('tempdb')
--AND		CHECKDB2.Value < '20210930'
ORDER BY CHECKDB1.Value;
 
DROP TABLE #temp;
GO
